apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'

group = "$groupId"
version = "$versionName"

android {
    compileSdkVersion = compileVersion
    namespace = "com.kylecorry.andromeda.bitmaps"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion targetVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17"
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/LICENSE-notice.md'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = group
                artifactId = 'bitmaps'
                version = version
            }
        }
    }
}

dependencies {
    implementation project(':core')
    implementation libs.androidx.core.ktx
    api libs.sol
    coreLibraryDesugaring libs.desugar
    testImplementation libs.junit
    testImplementation libs.junit.platform.runner
    testImplementation libs.junit.jupiter.api
    testImplementation libs.junit.jupiter.params
    androidTestImplementation libs.androidx.junit
    testRuntimeOnly libs.junit.jupiter.engine
    androidTestImplementation libs.androidx.espresso.core
}